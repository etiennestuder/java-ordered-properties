plugins {
  id 'com.gradle.build-scan' version '1.16'
  id 'nu.studer.credentials' version '1.0.5'
  id 'com.jfrog.bintray' version '1.8.4'
  id 'maven-publish'
  id 'groovy'
}

repositories {
  jcenter()
}

dependencies {
  testImplementation 'org.codehaus.groovy:groovy-all:2.4.16'
  testImplementation('org.spockframework:spock-core:1.0-groovy-2.4') {
    exclude module: 'groovy-all'
  }
}

group = 'nu.studer'
version = '1.0.2.DEV'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

task sourcesJar(type: Jar) {
  description = 'Assembles a jar archive containing the main source code.'
  group = 'build'
  from sourceSets.main.allSource
}

task docsJar(type: Jar) {
  description = 'Assembles a jar archive containing the documentation for the main source code.'
  group = 'build'
  from javadoc
}

publishing {
  publications {
    java(MavenPublication) {
      from components.java
      artifact sourcesJar {
        classifier 'sources'
      }
      artifact docsJar {
        classifier 'javadoc'
      }
    }
  }
}

bintray {
  user = credentials.BINTRAY_USER
  key = credentials.BINTRAY_API_KEY

  publications = ['java']

  dryRun = true
  publish = true

  pkg {
    repo = 'java-utilities'
    name = 'java-ordered-properties'
    desc = 'Alternative to the JDK class java.util.Properties.'
    websiteUrl = 'https://github.com/etiennestuder/java-ordered-properties'
    issueTrackerUrl = 'https://github.com/etiennestuder/java-ordered-properties/issues'
    vcsUrl = 'https://github.com/etiennestuder/java-ordered-properties.git'
    licenses = ['Apache-2.0']
    labels = ['java', 'utilities']
    publicDownloadNumbers = false

    version {
      vcsTag = publishing.publications.java.version
    }
  }
}

