plugins {
    id 'org.nosphere.gradle.github.actions' version '1.4.0'
    id 'maven-publish'
    id 'groovy'
}

group = 'nu.studer'
version = '1.0.5-DEV'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation('org.spockframework:spock-core:2.3-groovy-3.0')
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
        vendor = JvmVendorSpec.BELLSOFT
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(AbstractCompile).configureEach {
    options.compilerArgs <<
        "-Werror" <<
        "-Xlint:all" <<
        "-Xlint:-serial"
}

def dvBuildScan = develocity.buildScan
tasks.withType(Test).configureEach {
    maxParallelForks = 1 // there is currently only a single test class
    useJUnitPlatform()

    String testJavaRuntimeVersion = findProperty('testJavaRuntimeVersion') ?: '8'

    javaLauncher.set(javaToolchains.launcherFor { spec ->
        spec.languageVersion.set(JavaLanguageVersion.of(testJavaRuntimeVersion))
        dvBuildScan.value(identityPath.path + "#jvmVersion", testJavaRuntimeVersion)
    } as Provider<? extends JavaLauncher>)

    if (testJavaRuntimeVersion.toInteger() >= 16) { // required due to tightened reflective access in JDK 16 and higher
        jvmArgs '--add-opens', 'java.base/java.util=ALL-UNNAMED'
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
    publications {
        java(MavenPublication) {
            from components.java
            pom {
                name = 'java-ordered-properties'
                description = 'Alternative to the JDK class java.util.Properties'
                url = 'https://github.com/etiennestuder/java-ordered-properties'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'etiennestuder'
                        name = 'Etienne Studer'
                        email = 'etienne@studer.nu'
                    }
                }
                scm {
                    url = 'https://github.com/etiennestuder/java-ordered-properties.git'
                }
            }
        }
    }
}
